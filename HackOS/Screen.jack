class Screen {
  static int SCREEN_START;
  static int SCREEN_END;
  static Array bitMask;
  static boolean COLOR;
  
  function void init() {
    let SCREEN_START = 16384;
    let SCREEN_END = 24575;
    let COLOR = true; // true = black, false = white
    let bitMask = Math.power2Array();
    return;
  }

  function void setColor(boolean color) {
    let COLOR = color;
    return;
  }

  function void clearScreen() {
    do Memory.clearRAMRange(SCREEN_START, SCREEN_END);
    return;
  }

  function void drawPixel(int x, int y) {
    var int address, x16thBit, value;
    let address = (x / 16) + (y * 32) + SCREEN_START;
    let x16thBit = bitMask[Math.modPositive(x, 16)];
    do Screen.setBitsAtAddress(address, x16thBit);
    return;
  }

  function void drawLine(int x1, int y1, int x2, int y2) {
    var int diff, temp, dx, dy, a, b;
    if (x1 = x2) {
      do Screen.drawVerticalLine(x1, y1, y2);
      return;
    }

    if (y1 = y2) {
      do Screen.drawHorizontalLine(y1, x1, x2);
      return;
    }

    if (x1 > x2) {
      let temp = x1;
      let x1 = x2;
      let x2 = temp;
    }

    if (y1 > y2) {
      let temp = y1;
      let y1 = y2;
      let y2 = temp;
    }

    let dx = x2 - x1;
    let dy = y2 - y1;
    let a = 0;
    let b = 0;
    let diff = 0;
    while ( ((a < dx) | (a = dx)) & ((b < dy) | (b = dy)) ) {
      do Screen.drawPixel(x1 + a, y1 + b);
      if (diff < 0) {
        let a = a + 1;
        let diff = diff + dy;
      }
      else {
        let b = b + 1;
        let diff = diff - dx;
      }
    }

    return;
  }

  function void drawVerticalLine(int x, int y1, int y2) {
    var int temp, x16thBit, address;
    if (y1 > y2) {
      let temp = y2;
      let y2 = y1;
      let y1 = temp;
    }

    let x16thBit = bitMask[Math.modPositive(x, 16)];
    let address = (x / 16) + (y1 * 32) + SCREEN_START;
    while ((y1 < y2) | (y1 = y2)) {
      do Screen.setBitsAtAddress(address, x16thBit);
      let address = address + 32;
      let y1 = y1 + 1;
    }

    return;
  }

  function void drawHorizontalLine(int y, int x1, int x2) {
    var int temp, currentWord, currentBit, addressBase;
    if (x1 > x2) {
      let temp = x2;
      let x2 = x1;
      let x1 = temp;
    }

    let currentWord = x1 / 16;
    let currentBit = Math.modPositive(x1, 16);
    let addressBase = (y * 32) + SCREEN_START;
    do Screen._horizontalLineLoop(y, x1, x2, currentWord, currentBit, addressBase);
    return;
  }

  function void _horizontalLineLoop(int y, int x1, int x2, int currentWord, int currentBit, int addressBase) {
    while ((x1 < x2) | (x1 = x2)) {
      // If we are targeting the first bit of a word, then set the whole word to 16 (1|0)s as long as the entire word is a part of the line.
      // NOTE: true = -1, false = 0 in the Jack language.
      if ((currentBit = 0) & ((x2 - x1) > 15)) {
        do Memory.poke(addressBase + currentWord, COLOR);
        let x1 = x1 + 16;
        let currentWord = currentWord + 1;
      }
      else {
        do Screen.setBitsAtAddress(addressBase + currentWord, bitMask[currentBit]);
        if (currentBit = 15) {
          let currentBit = 0;
          let currentWord = currentWord + 1;
        }
        else {
          let currentBit = currentBit + 1;
        }

        let x1 = x1 + 1;
      }
    }

    return;
  }

  function void drawRectangle(int x1, int y1, int x2, int y2) {
    var int temp, currentWord, currentBit, addressBase, length;
    if (x1 > x2) {
      let temp = x1;
      let x1 = x2;
      let x2 = temp;
    }

    if (y1 > y2) {
      let temp = y1;
      let y1 = y2;
      let y2 = temp;
    }

    let currentWord = x1 / 16;
    let currentBit = Math.modPositive(x1, 16);
    let addressBase = (y1 * 32) + SCREEN_START;
    while ((y1 < y2) | (y1 = y2)) {
      do Screen._horizontalLineLoop(y1, x1, x2, currentWord, currentBit, addressBase);
      let y1 = y1 + 1;
      let addressBase = addressBase + 32;
    }

    return;
  }

  function void drawCircle(int x, int y, int r) {
    var int dy, r2, dy2, length;
    let r = Math.min(r, 181);
    let r2 = r * r;
    let dy = -r;
    let dy2 = dy * dy;
    while ((dy < r) | (dy = r)) {
      let length = Math.sqrt(r2 - dy2);
      do Screen.drawHorizontalLine(y + dy, x - length, x + length);
      do Screen.drawHorizontalLine(y + r, x - length, x + length);
      let dy = dy + 1;
      let dy2 = dy2 + dy + (dy + 1);
      let r = r - 1;
    }

    return;
  }

  function void setBitsAtAddress(int address, int bit) {
    var int value;
    let value = Memory.peek(address);
    // When COLOR = false, then the color painted must be white, which means no bit set at that particular pixel.
    if (~COLOR) {
      let value = value & ~bit;
    }
    else {
      let value = value | bit;
    }

    do Memory.poke(address, value);
    return;
  }
}
